plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'net.ltgt.errorprone' version '4.1.0'
	id 'info.solidsoft.pitest' version '1.19.0-rc.2'
}

group = 'de.bas.bodo'
version = '0.0.1-SNAPSHOT'
description = 'GenAi Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url = 'https://repo.spring.io/snapshot' }
}

ext {
	set('springAiVersion', "1.1.2")
	set('springModulithVersion', "1.4.6")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.ai:spring-ai-starter-model-openai'
	implementation 'org.springframework.modulith:spring-modulith-starter-core'
	compileOnly 'org.jspecify:jspecify:1.0.0'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	errorprone 'com.google.errorprone:error_prone_core:2.36.0'
	errorprone 'com.uber.nullaway:nullaway:0.12.5'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
		mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	options.errorprone {
		check('NullAway', net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
		option('NullAway:AnnotatedPackages', 'de.bas.bodo.genai')
	}
}

jacoco {
	toolVersion = '0.8.13'
}

tasks.named('jacocoTestReport') {
	dependsOn tasks.test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

pitest {
	junit5PluginVersion = '1.0.0'
	targetClasses = ['de.bas.bodo.genai.*']
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
}
